// If she's not Italian, is she a Vegatarian?

Sort FOOD
Sort BOOL

Name T/0 -> BOOL
Name roo/0 -> FOOD

Var x -> FOOD

Fun Aussie/0 -> BOOL
Fun Italian/0 -> BOOL
Fun Eats/1 -> BOOL
Fun Meat/1 -> BOOL
Fun Veggie/0 -> BOOL

Let falsum := T /= T
Let truth := ~falsum

// The following conditionals are all the agent believes. In the formal logic,
// there is just one pair of split levels for this only-believing. In this
// implementation, there is one such pair of split levels per each conditional.
// The only reason for this departure from the theory is that it was a little
// less work to implement.

// Most Aussies are not Italian, and vice-versa.
KB: Bel<1,1> Aussie = T ==> Italian != T
KB: Bel<1,1> Italian = T ==> Aussie != T

// Most Aussies eat Kangaroo meat.
KB: Bel<1,1> Aussie = T ==> Eats(roo) = T

// She's presumably Italian or a Vegetarian, and
// if she's not Italian, she's probably Australian.
KB: Bel<1,1> truth ==> (Italian = T || Veggie = T)
KB: Bel<1,1> Italian != T ==> Aussie = T

// Kangaroo is definitely meat, and Vegetarians don't eat meat.
KB: Bel<1,1> Meat(roo) != T ==> falsum
KB: Bel<1,1> ~Fa x ((Veggie == T && Meat(x) == T) -> Eats(x) != T) ==> falsum


// The question is now:
// If she's not Italian, is she a Vegetarian?
// We find that out at belief levels 1, 1.
Refute: Bel<0,0> Italian != T ==> Veggie != T
Refute: Bel<0,1> Italian != T ==> Veggie != T
Refute: Bel<1,0> Italian != T ==> Veggie != T
Assert: Bel<1,1> Italian != T ==> Veggie != T

